#Использовать "../internal"

Перем Cookies Экспорт;
Перем Аутентификация Экспорт;
Перем Метод Экспорт;
Перем Заголовки Экспорт;
Перем ПараметрыЗапроса Экспорт;
Перем URL Экспорт;
Перем ПараметрыПреобразованияJSON Экспорт;
Перем HTTPЗапрос Экспорт;

Процедура ПриСозданииОбъекта(Сессия, МетодHTTP, СтрокаURL, ДополнительныеПараметры)

	Метод = МетодHTTP;
	Cookies = КоннекторHTTPСлужебный.ВыбратьЗначение(Неопределено, ДополнительныеПараметры, "Cookies", Новый Массив);
	CookiesИзСессии = КоннекторHTTPСлужебный.ДозаполнитьCookie(Сессия.Cookies, СтрокаURL);
	Cookies = КоннекторHTTPСлужебный.ДозаполнитьCookie(Cookies, СтрокаURL);
	Cookies = КоннекторHTTPСлужебный.ОбъединитьCookies(CookiesИзСессии, Cookies);
	
	АутентификацияИзДополнительныхПараметров =
		КоннекторHTTPСлужебный.ВыбратьЗначение(Неопределено, ДополнительныеПараметры, "Аутентификация", Новый Структура);
	ПараметрыЗапросаИзДополнительныхПараметров =
		КоннекторHTTPСлужебный.ВыбратьЗначение(Неопределено, ДополнительныеПараметры, "ПараметрыЗапроса", Новый Структура);
	ЗаголовкиИзДополнительныхПараметров =
		КоннекторHTTPСлужебный.ВыбратьЗначение(Неопределено, ДополнительныеПараметры, "Заголовки", Новый Соответствие);

	Аутентификация = КоннекторHTTPСлужебный.ОбъединитьПараметрыАутентификации(
		АутентификацияИзДополнительныхПараметров, Сессия.Аутентификация);
	ПараметрыЗапроса = КоннекторHTTPСлужебный.ОбъединитьПараметрыЗапроса(
		ПараметрыЗапросаИзДополнительныхПараметров, Сессия.ПараметрыЗапроса);
	Заголовки = КоннекторHTTPСлужебный.ОбъединитьЗаголовки(ЗаголовкиИзДополнительныхПараметров, Сессия.Заголовки);
	ПараметрыПреобразованияJSON = КоннекторHTTPСлужебный.ВыбратьЗначение(
		Неопределено, ДополнительныеПараметры, "ПараметрыПреобразованияJSON", Неопределено);
	URL = КоннекторHTTPСлужебный.ПодготовитьURL(СтрокаURL, ПараметрыЗапроса);
	
	КоннекторHTTPСлужебный.ПодготовитьCookies(ЭтотОбъект);

	Данные = КоннекторHTTPСлужебный.ВыбратьЗначение(
		Неопределено, ДополнительныеПараметры, "Данные", Новый Структура);
	Файлы = КоннекторHTTPСлужебный.ВыбратьЗначение(
		Неопределено, ДополнительныеПараметры, "Файлы", Новый Массив);
	Json = КоннекторHTTPСлужебный.ВыбратьЗначение(
		Неопределено, ДополнительныеПараметры, "Json", Неопределено);
	ПараметрыЗаписиJSON = КоннекторHTTPСлужебный.ВыбратьЗначение(
		Неопределено, ДополнительныеПараметры, "ПараметрыЗаписиJSON", Неопределено);

	КоннекторHTTPСлужебный.ПодготовитьТелоЗапроса(ЭтотОбъект, Данные, Файлы, Json, ПараметрыЗаписиJSON);
	КоннекторHTTPСлужебный.ПодготовитьАутентификацию(ЭтотОбъект);
	
КонецПроцедуры